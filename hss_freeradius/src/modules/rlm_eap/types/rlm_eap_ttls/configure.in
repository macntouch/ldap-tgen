AC_INIT(rlm_eap_ttls.c)
AC_REVISION($Revision: 1.3 $)
AC_DEFUN(modname,[rlm_eap_ttls])

fail=
SMART_LIBS=
SMART_CLFAGS=
if test x$with_[]modname != xno; then

	AC_PROG_CC

	dnl extra argument: --with-rlm-eap-ttls-lib-dir
	rlm_eap_ttls_lib_dir=
	AC_ARG_WITH(rlm-eap-ttls-lib-dir,
	[  --with-rlm-eap-ttls-lib-dir=DIR   Directory for EAPTTLS library files []],
	[ case "$withval" in
	    no)
		AC_MSG_ERROR(Need rlm-eap-ttls-lib-dir)
		;;
	    yes)
		;;
	    *)
		rlm_eap_ttls_lib_dir="$withval"
		;;
	  esac ]
	)

	dnl extra argument: --with-rlm-eap-ttls-include-dir
	rlm_eap_ttls_inc_dir=
	AC_ARG_WITH(rlm-eap-ttls-include-dir,
	[ --with-rlm-eap-ttls-include-dir=DIR Directory for EAPTTLS include files []],
	[ case "$withval" in
	    no)
		AC_MSG_ERROR(Need rlm-eap-ttls-include-dir)
		;;
	    yes)
		;;
	    *)
		rlm_eap_ttls_inc_dir="$withval"
		;;
	  esac ]
	)

	dnl #
	dnl # extra argument: --openssl-includes=dir
	dnl #
	AC_ARG_WITH(openssl-includes,
	[  --with-openssl-includes=DIR       Directory to look for OpenSSL include files],
	[ case "$withval" in
	    *)
		rlm_eap_ttls_inc_dir="$rlm_eap_ttls_inc_dir $withval"
		SMART_CFLAGS="$SMART_CFLAGS -I$withval"
		;;
	  esac ]
	)

	dnl #
	dnl # extra argument: --openssl-libraries=dir
	dnl #
	AC_ARG_WITH(openssl-libraries,
	[  --with-openssl-libraries=DIR      Directory to look for OpenSSL library files],
	[ case "$withval" in
	    *)
		rlm_eap_ttls_lib_dir="$rlm_eap_ttls_lib_dir $withval"
		SMART_LIBS="$SMART_LIBS -L$withval"
		;;
	  esac ]
	)

	smart_try_dir=$rlm_eap_ttls_inc_dir
	AC_SMART_CHECK_INCLUDE(openssl/ssl.h)
	if test "x$ac_cv_header_openssl_ssl_h" != "xyes"; then
		fail="$fail (openssl/ssl.h)"
	fi

	smart_try_dir=$rlm_eap_ttls_lib_dir
	AC_SMART_CHECK_LIB(crypto, DH_new)
    if test "x$ac_cv_lib_crypto_DH_new" != "xyes"; then
 	    fail="$fail libcrypto"
    fi

	AC_SMART_CHECK_LIB(ssl, SSL_new)
    if test "x$ac_cv_lib_ssl_SSL_new" != "xyes"; then
 	    fail="$fail libssl"
    fi

AC_CHECK_HEADERS( \
	openssl/err.h \
	openssl/engine.h
	)

	targetname=modname
else
	targetname=
	echo \*\*\* module modname is disabled.
fi

if test x"$fail" != x""; then
	if test x"${enable_strict_dependencies}" = x"yes"; then
		AC_MSG_ERROR([set --without-]modname[ to disable it explicitly.])
	else
		AC_MSG_WARN([silently not building ]modname[.])
		AC_MSG_WARN([FAILURE: ]modname[ requires: $fail.])
		if test x"$headersuggestion" != x; then
			AC_MSG_WARN([$headersuggestion]) 
		fi
		if test x"$libsuggestion" != x; then
			AC_MSG_WARN([$libsuggestion]) 
		fi
		targetname=""
	fi
fi

eap_ttls_ldflags=$SMART_LIBS
eap_ttls_cflags=$SMART_CFLAGS
AC_SUBST(eap_ttls_ldflags)
AC_SUBST(eap_ttls_cflags)
AC_SUBST(targetname)
AC_CONFIG_HEADER([config.h])
AC_OUTPUT(Makefile)
